/* 
 * Analizador Sintáctico con Análisis Semántico Integrado + Código Intermedio
 * --------------------------------------------------------------------------
 * 
 * 
 * CAMBIOS PARA PROYECTO 2:
 * - Manejo de scopes y tabla de símbolos semántica
 * - Verificación de tipos y compatibilidad
 * - Detección de errores semánticos
 * 
 */

/* Imports y paquetes */
import java_cup.runtime.*;
import java.util.*;
import main.java.symbol.SymbolTable;
import main.java.symbol.SemanticSymbolTable;
import main.java.intermedio.IntermediateCodeGenerator;

/* Código de usuario */
parser code {:

    private SemanticSymbolTable semanticTable;
    
    private SymbolTable symbolTable;
    
    private int errorCount = 0;

    private Stack<String> scopeContext = new Stack<>();
    
    private String currentFunctionName = null;
    

    private IntermediateCodeGenerator codeGenerator;
    private boolean codeGenerationEnabled = false;
    
    public void enableCodeGeneration(String outputFile) {
        this.codeGenerator = new IntermediateCodeGenerator(outputFile);
        this.codeGenerationEnabled = true;
        System.out.println("Generación de código intermedio habilitada");
    }
    
    public void disableCodeGeneration() {
        this.codeGenerationEnabled = false;
        this.codeGenerator = null;
    }
    
    public IntermediateCodeGenerator getCodeGenerator() {
        return codeGenerator;
    }

    public void initTables() {
        semanticTable = new SemanticSymbolTable();
        symbolTable = semanticTable.getOriginalTable();
        System.out.println("Iniciando analisis semantico...");
        
        scopeContext.push("GLOBAL");
    }
    
    public void setSymbolTable(SymbolTable symbolTable) {
        this.symbolTable = symbolTable;
        if (semanticTable == null) {
            initTables();
        }
    }
    
    public SemanticSymbolTable getSemanticTable() {
        if (semanticTable == null) {
            initTables();
        }
        return semanticTable;
    }

    public String getCurrentFunctionName() {
        return currentFunctionName;
    }
    
    public void setCurrentFunctionName(String functionName) {
        this.currentFunctionName = functionName;
    }
    
    public void enterFunctionScope(String functionName, String returnType, List<String> params, int line, int column) {
        if (semanticTable != null) {
            semanticTable.enterScope("FUNCTION", functionName);
            scopeContext.push("FUNCTION:" + functionName);
            setCurrentFunctionName(functionName); 
            System.out.println("Entrando a función: " + functionName);
        }
    }
    
    public void exitFunctionScope() {
        if (semanticTable != null && !scopeContext.isEmpty()) {
            String context = scopeContext.pop();
            semanticTable.exitScope();
            setCurrentFunctionName(null);
            System.out.println("Saliendo de scope: " + context);
        }
    }
    
    public void enterBlockScope() {
        if (semanticTable != null) {
            semanticTable.enterScope("BLOCK", "block");
            scopeContext.push("BLOCK");
            System.out.println("Entrando a bloque...");
        }
    }
    
    public void exitBlockScope() {
        if (semanticTable != null && !scopeContext.isEmpty()) {
            String context = scopeContext.pop();
            semanticTable.exitScope();
            System.out.println("Saliendo de bloque...");
        }
    }
    
    public void enterControlScope(String controlType) {
        if (semanticTable != null) {
            semanticTable.enterScope(controlType.toUpperCase(), controlType.toLowerCase() + "_block");
            scopeContext.push(controlType.toUpperCase());
            System.out.println("Entrando a " + controlType + "...");
        }
    }
    
    public void exitControlScope() {
        if (semanticTable != null && !scopeContext.isEmpty()) {
            String context = scopeContext.pop();
            semanticTable.exitScope();
            System.out.println("Saliendo de control: " + context);
        }
    }
    
    public void finalizeSemantic() {
        if (semanticTable != null) {
            boolean hasMain = semanticTable.checkMainFunction();
            if (!hasMain) {
                errorCount++;
            }
            
            semanticTable.printSummary();
            
            try {
                semanticTable.escribirTablas("semantic_analysis.txt");
                System.out.println("Analisis semantico completado. Ver: semantic_analysis.txt");
            } catch (Exception e) {
                System.err.println("Error escribiendo archivo semantico: " + e.getMessage());
            }
        }
        
        if (codeGenerationEnabled && codeGenerator != null) {
            codeGenerator.printCode();
            codeGenerator.printStatistics();
            codeGenerator.writeToFile();
        }
    }
    
    /* Métodos de manejo de errores existentes */
    public void syntax_error(Symbol s) {
        errorCount++;
        System.err.println("Error sintactico #" + errorCount + " en linea " + (s.left + 1) + 
                          ", columna " + (s.right + 1) + ": Token inesperado '" + s.value + "'");
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception {
        errorCount++;
        System.err.println("Error sintactico fatal #" + errorCount + " en linea " + (s.left + 1) + 
                          ", columna " + (s.right + 1) + ": No se pudo recuperar del error");
        
        System.err.println("Total de errores encontrados: " + errorCount);
        
        finalizeSemantic();
    }
    
    public void report_error(String message, Object info) {
        errorCount++;
        if (info instanceof Symbol) {
            Symbol s = (Symbol) info;
            System.err.println("Error de recuperacion #" + errorCount + " en linea " + (s.left + 1) + 
                              ", columna " + (s.right + 1) + ": " + message);
        } else {
            System.err.println("Error de recuperacion #" + errorCount + ": " + message);
        }
    }
    
    public int getErrorCount() {
        return errorCount + (semanticTable != null ? semanticTable.getErrorCount() : 0);
    }
    
    /* Métodos de compatibilidad existentes */
    public void updateVariableType(String id, String type) {
        if (symbolTable != null) {
            symbolTable.actualizarTipoVariable(id, type);
        }
    }
    
    public void markAsFunction(String id, String returnType) {
        if (symbolTable != null) {
            symbolTable.marcarComoFuncion(id, returnType);
        }
    }
:}

non terminal program;
non terminal block;
non terminal decl_var, decl_fun, decl_arr;
non terminal stmt;
non terminal assign;
non terminal String exp;              
non terminal String exp_arit, term_arit, factor_arit, power_arit, atom_arit;
non terminal String exp_rel;          
non terminal String exp_log, term_log, factor_log;
non terminal String unary_exp;        
non terminal ctrl, if_stmt, do_while, for_stmt;
non terminal break_stmt, return_stmt;
non terminal read_stmt, write_stmt;
non terminal String tipo, tipo_r;     
non terminal String arr_access;      
non terminal exp_list, exp_matrix, matrix_row;
non terminal param, param_list, arg_list;
non terminal String literal;         
non terminal String llamada_fun;     
non terminal switch_stmt, case_list, case_stmt, default_stmt;

terminal IF, ELIF, ELSE, DO, WHILE, FOR, BREAK, RETURN;
terminal INT, FLOAT, BOOL, CHAR, STRING, VOID;
terminal READ, WRITE, MAIN, TRUE, FALSE;
terminal PLUS, MINUS, TIMES, DIVIDE, MOD, POW;
terminal INCREMENT, DECREMENT;
terminal LT, LTE, GT, GTE, EQ, NEQ;
terminal AND, OR, NOT;
terminal LPAREN, RPAREN, LBLOCK, RBLOCK;
terminal QUESTION, COMMA, ASSIGN;
terminal LBRACKET, RBRACKET;
terminal UMINUS;
terminal ID, LIT_INT, LIT_FLOAT, LIT_BOOL, LIT_CHAR, LIT_STRING;
terminal SWITCH, CASE, DEFAULT, COLON;

precedence left OR;
precedence left AND;
precedence right NOT;
precedence nonassoc LT, LTE, GT, GTE, EQ, NEQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE, MOD;
precedence right POW;
precedence right UMINUS, INCREMENT, DECREMENT;
precedence left LPAREN, RPAREN, LBRACKET, RBRACKET;

/* 
 * Gramática con recuperación de errores y análisis semántico
 */
start with program;

/* 
 * Estructura del programa con manejo correcto de main
 */
program ::= VOID MAIN:m LPAREN RPAREN LBLOCK 
          {: 
             List<String> noParams = new ArrayList<String>();
             parser.getSemanticTable().declareFunction("main", "VOID", noParams, mleft, mright);
             
             parser.getSemanticTable().enterScope("FUNCTION", "main");
             parser.setCurrentFunctionName("main");
             System.out.println("Entrando a función main");
             
             if (parser.codeGenerationEnabled) {
                 parser.getCodeGenerator().startFunction("main", "VOID");
             }
          :}
          block RBLOCK
          {: 
             parser.getSemanticTable().exitScope();
             parser.setCurrentFunctionName(null);
             System.out.println("Saliendo de función main");
             
             if (parser.codeGenerationEnabled) {
                 parser.getCodeGenerator().endFunction("main");
             }
             
             parser.finalizeSemantic();
          :}
          | decl_fun:f program
          {: 
             if (f != null) {
                 System.out.println("Función declarada: " + f.toString());
             }
          :}
          | error program  
          {: 
             System.err.println("Error en la estructura del programa. Continuando análisis..."); 
          :}
          | /* programa vacío */
          ;

/* 
 * Declaración de funciones con análisis semántico y scope correcto
 */
decl_fun ::= tipo_r:t ID:id LPAREN param_list RPAREN LBLOCK 
           {: 
              List<String> params = new ArrayList<String>(); 
              parser.getSemanticTable().declareFunction(id.toString(), t.toString(), params, idleft, idright);
              
              parser.getSemanticTable().enterScope("FUNCTION", id.toString());
              parser.setCurrentFunctionName(id.toString());
              System.out.println("Entrando a función: " + id.toString() + " tipo: " + t.toString());
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().startFunction(id.toString(), t.toString());
              }
           :}
           block RBLOCK
           {: 
              RESULT = id; 
              
              parser.getSemanticTable().exitScope();
              parser.setCurrentFunctionName(null);
              System.out.println("Saliendo de función: " + id.toString());
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().endFunction(id.toString());
              }
              
              parser.markAsFunction(id.toString(), t.toString());
           :}
           | tipo_r:t ID:id LPAREN RPAREN LBLOCK 
           {: 
              List<String> noParams = new ArrayList<String>();
              parser.getSemanticTable().declareFunction(id.toString(), t.toString(), noParams, idleft, idright);
              
              parser.getSemanticTable().enterScope("FUNCTION", id.toString());
              parser.setCurrentFunctionName(id.toString());
              System.out.println("Entrando a función sin parámetros: " + id.toString() + " tipo: " + t.toString());
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().startFunction(id.toString(), t.toString());
              }
           :}
           block RBLOCK
           {: 
              RESULT = id; 
              
              parser.getSemanticTable().exitScope();
              parser.setCurrentFunctionName(null);
              System.out.println("Saliendo de función: " + id.toString());
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().endFunction(id.toString());
              }
              
              parser.markAsFunction(id.toString(), t.toString()); 
           :}
           ;

/* 
 * Tipos de retorno para funciones
 */
tipo_r ::= INT {: RESULT = "INT"; :}
         | FLOAT {: RESULT = "FLOAT"; :}
         | CHAR {: RESULT = "CHAR"; :}
         | BOOL {: RESULT = "BOOL"; :}
         | VOID {: RESULT = "VOID"; :}
         ;

/* 
 * Lista de parámetros de función
 */
param_list ::= param
             | param_list COMMA param
             ;

/* 
 * Parámetro individual con análisis semántico
 */
param ::= tipo:t ID:id 
        {: 
           if (parser.getSemanticTable() != null) {
               parser.getSemanticTable().declareVariable(id.toString(), t.toString(), idleft, idright);
           }
           
           if (parser.codeGenerationEnabled) {
               parser.getCodeGenerator().declareVariable(id.toString(), t.toString());
           }
           
           parser.updateVariableType(id.toString(), t.toString());
        :}
        ;

/* 
 * Bloque de código
 */
block ::= stmt block
        | error block  {: System.err.println("Error en bloque de código. Continuando..."); :}
        | /* bloque vacío */
        ;

/* 
 * Tipos de datos
 */
tipo ::= INT {: RESULT = "INT"; :}
       | FLOAT {: RESULT = "FLOAT"; :}
       | BOOL {: RESULT = "BOOL"; :}
       | CHAR {: RESULT = "CHAR"; :}
       | STRING {: RESULT = "STRING"; :}
       ;

/* 
 * Declaración de arreglos con análisis semántico
 */
decl_arr ::= tipo:t ID:id LBRACKET exp:e1 RBRACKET LBRACKET exp:e2 RBRACKET QUESTION
           {: 
              String arrayType = t.toString() + "[][]";
              
              if (parser.getSemanticTable() != null) {
                  parser.getSemanticTable().declareVariable(id.toString(), arrayType, idleft, idright);
              }
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().declareVariable(id.toString(), arrayType);
              }
              
              parser.updateVariableType(id.toString(), arrayType);
           :}
           | tipo:t ID:id LBRACKET exp:e1 RBRACKET LBRACKET exp:e2 RBRACKET ASSIGN exp_matrix QUESTION
           {: 
              String arrayType = t.toString() + "[][]";
              
              if (parser.getSemanticTable() != null) {
                  parser.getSemanticTable().declareVariable(id.toString(), arrayType, idleft, idright);
              }
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().declareVariable(id.toString(), arrayType);
                  parser.getCodeGenerator().addComment("Array inicializado con matriz");
              }
              
              parser.updateVariableType(id.toString(), arrayType);
           :}
           ;

/* Estructura de matriz */
exp_matrix ::= LBRACKET matrix_row RBRACKET
             | LBRACKET matrix_row COMMA matrix_row RBRACKET
             ;

/* Fila de matriz */
matrix_row ::= LBRACKET exp_list RBRACKET;

/* Lista de expresiones */
exp_list ::= exp
           | exp COMMA exp_list
           ;

/* Acceso a arreglos con verificación semántica */
arr_access ::= ID:id LBRACKET exp:e1 RBRACKET LBRACKET exp:e2 RBRACKET
             {: 
                if (parser.getSemanticTable() != null) {
                }
                
                if (parser.codeGenerationEnabled) {
                    RESULT = parser.getCodeGenerator().generateArrayAccess(id.toString(), e1, e2);
                } else {
                    RESULT = "ARRAY_ACCESS"; 
                }
             :}
             ;

/* 
 * Sentencias con manejo semántico de bloques
 */
stmt ::= exp:e QUESTION
       {: 
          if (parser.getSemanticTable() != null && e != null) {
          }
       :}
       | assign
       | decl_var
       | decl_arr
       | ctrl
       | break_stmt
       | return_stmt
       | read_stmt
       | write_stmt
       | LBLOCK block RBLOCK
       {: 
          parser.enterBlockScope();
          parser.exitBlockScope();
       :}
       | error QUESTION  {: System.err.println("Error en sentencia. Recuperando en ';'..."); :}
       ;

/* 
 * Asignación con verificación de tipos 
 */
assign ::= ID:id ASSIGN exp:e QUESTION
         {:
            boolean valid = parser.getSemanticTable().checkAssignment(id.toString(), e, idleft);
            
            if (valid) {
                System.out.println("Asignacion valida: " + id + " = " + e);
            } else {
                System.err.println("Error en asignacion a variable: " + id);
            }
            
            if (parser.codeGenerationEnabled && valid) {
                parser.getCodeGenerator().generateAssignment(id.toString(), e);
            }
         :}
         | arr_access:arr ASSIGN exp:e QUESTION
         {: 
            if (parser.getSemanticTable() != null) {
                System.out.println("Asignacion a array: " + arr + " = " + e);
                
                if (parser.codeGenerationEnabled) {
                    parser.getCodeGenerator().addComment("Asignación a elemento de array");
                }
            }
         :}
         | ID ASSIGN error QUESTION  {: System.err.println("Error en expresión de asignación. Continuando..."); :}
         ;

/* 
 * Declaración de variables con verificación semántica completa
 */
decl_var ::= tipo:t ID:id ASSIGN exp:e QUESTION
           {: 
              boolean declared = parser.getSemanticTable().declareVariable(id.toString(), t, idleft, idright, true);
              
              if (declared) {
                  if (e != null) {
                      boolean compatible = parser.getSemanticTable().checkTypeCompatibility(t, e, idleft);
                      if (compatible) {
                          System.out.println("Variable '" + id + "' declarada e inicializada correctamente como " + t);
                      } else {
                          System.err.println("Error: tipos incompatibles en inicializacion de " + id);
                      }
                  }
              }
              
              parser.updateVariableType(id.toString(), t.toString());
              
              if (parser.codeGenerationEnabled && declared) {
                  parser.getCodeGenerator().declareVariable(id.toString(), t.toString());
                  if (e != null) {
                      parser.getCodeGenerator().generateAssignment(id.toString(), e);
                  }
              }
           :}
           | tipo:t ID:id QUESTION
           {: 
              boolean declared = parser.getSemanticTable().declareVariable(id.toString(), t, idleft, idright, false);
              
              if (declared) {
                  System.out.println("Variable '" + id + "' declarada como " + t + " (sin inicializar)");
              }
              
              parser.updateVariableType(id.toString(), t.toString());
              
              if (parser.codeGenerationEnabled && declared) {
                  parser.getCodeGenerator().declareVariable(id.toString(), t.toString());
              }
           :}
           ;

/* Valores literales con tipos semánticos */
literal ::= LIT_INT:val
          {: 
             RESULT = "INT"; 
             System.out.println("Literal entero detectado");
             
             if (parser.codeGenerationEnabled) {
                 RESULT = val.toString(); 
             }
          :}
         | LIT_FLOAT:val
          {: 
             RESULT = "FLOAT"; 
             System.out.println("Literal flotante detectado");
             
             if (parser.codeGenerationEnabled) {
                 RESULT = val.toString();
             }
          :}
         | LIT_BOOL:val
          {: 
             RESULT = "BOOL"; 
             System.out.println("Literal booleano detectado");
             
             if (parser.codeGenerationEnabled) {
                 RESULT = val.toString();
             }
          :}
         | LIT_CHAR:val
          {: 
             RESULT = "CHAR"; 
             System.out.println("Literal caracter detectado");
             
             if (parser.codeGenerationEnabled) {
                 RESULT = val.toString();
             }
          :}
         | LIT_STRING:val
          {: 
             RESULT = "STRING"; 
             System.out.println("Literal cadena detectado");
             
             if (parser.codeGenerationEnabled) {
                 RESULT = val.toString();
             }
          :}
         | TRUE 
          {: 
             RESULT = "BOOL"; 
             System.out.println("Literal booleano 'true' detectado");
             
             if (parser.codeGenerationEnabled) {
                 RESULT = "true";
             }
          :}
         | FALSE 
          {: 
             RESULT = "BOOL"; 
             System.out.println("Literal booleano 'false' detectado");
             
             if (parser.codeGenerationEnabled) {
                 RESULT = "false";
             }
          :}
         ;

/* Llamadas a funciones con verificación semántica */
llamada_fun ::= ID:id LPAREN arg_list RPAREN
              {: 
                 if (parser.getSemanticTable() != null) {
                     RESULT = "FUNCTION_CALL"; 
                 }
                 
                 if (parser.codeGenerationEnabled) {
                     List<String> args = new ArrayList<String>(); // TODO: extraer argumentos reales
                     RESULT = parser.getCodeGenerator().generateFunctionCall(id.toString(), args);
                 }
              :}
              | ID:id LPAREN RPAREN
              {: 
                 if (parser.getSemanticTable() != null) {
                     RESULT = "FUNCTION_CALL";
                 }
                 
                 if (parser.codeGenerationEnabled) {
                     List<String> noArgs = new ArrayList<String>();
                     RESULT = parser.getCodeGenerator().generateFunctionCall(id.toString(), noArgs);
                 }
              :}
              ;

/* Punto de entrada para expresiones con propagación de tipos */
exp ::= exp_log:e {: RESULT = e; :}
      ;

/* Expresiones lógicas con verificación de tipos */
exp_log ::= exp_log:e1 OR term_log:e2
          {: 
             if (parser.getSemanticTable() != null) {
             }
             RESULT = "BOOL"; 
             
             if (parser.codeGenerationEnabled) {
                 RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "||", e2);
             }
          :}
          | term_log:e {: RESULT = e; :}
          ;

term_log ::= term_log:e1 AND factor_log:e2
           {: 
              if (parser.getSemanticTable() != null) {
              }
              RESULT = "BOOL"; 
              
              if (parser.codeGenerationEnabled) {
                  RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "&&", e2);
              }
           :}
           | factor_log:e {: RESULT = e; :}
           ;

factor_log ::= NOT atom_arit:e
             {: 
                if (parser.getSemanticTable() != null) {
                }
                RESULT = "BOOL"; 
                
                if (parser.codeGenerationEnabled) {
                    RESULT = parser.getCodeGenerator().generateUnaryOp("!", e);
                }
             :}
             | exp_rel:e {: RESULT = e; :}
             | LPAREN exp_log:e RPAREN {: RESULT = e; :}
             ;

/* Expresiones relacionales con verificación de tipos */
exp_rel ::= exp_arit:e1 LT exp_arit:e2 
          {: 
             RESULT = "BOOL"; 
             
             if (parser.codeGenerationEnabled) {
                 RESULT = parser.getCodeGenerator().generateComparison(e1, "<", e2);
             }
          :}
          | exp_arit:e1 LTE exp_arit:e2 
          {: 
             RESULT = "BOOL"; 
             
             if (parser.codeGenerationEnabled) {
                 RESULT = parser.getCodeGenerator().generateComparison(e1, "<=", e2);
             }
          :}
          | exp_arit:e1 GT exp_arit:e2 
          {: 
             RESULT = "BOOL"; 
             
             if (parser.codeGenerationEnabled) {
                 RESULT = parser.getCodeGenerator().generateComparison(e1, ">", e2);
             }
          :}
          | exp_arit:e1 GTE exp_arit:e2 
          {: 
             RESULT = "BOOL"; 
             
             if (parser.codeGenerationEnabled) {
                 RESULT = parser.getCodeGenerator().generateComparison(e1, ">=", e2);
             }
          :}
          | exp_arit:e1 EQ exp_arit:e2 
          {: 
             RESULT = "BOOL"; 
             
             if (parser.codeGenerationEnabled) {
                 RESULT = parser.getCodeGenerator().generateComparison(e1, "==", e2);
             }
          :}
          | exp_arit:e1 NEQ exp_arit:e2 
          {: 
             RESULT = "BOOL"; 
             
             if (parser.codeGenerationEnabled) {
                 RESULT = parser.getCodeGenerator().generateComparison(e1, "!=", e2);
             }
          :}
          | exp_arit:e {: RESULT = e; :}
          ;

/* Expresiones aritméticas con propagación de tipos */
exp_arit ::= exp_arit:e1 PLUS term_arit:e2
           {: 
              RESULT = "NUMERIC"; 
              if (parser.codeGenerationEnabled) {
                  RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "+", e2);
              }
           :}
           | exp_arit:e1 MINUS term_arit:e2 
           {: 
              RESULT = "NUMERIC"; 
              
              if (parser.codeGenerationEnabled) {
                  RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "-", e2);
              }
           :}
           | term_arit:e {: RESULT = e; :}
           ;

term_arit ::= term_arit:e1 TIMES factor_arit:e2 
            {: 
               RESULT = "NUMERIC"; 
               
               if (parser.codeGenerationEnabled) {
                   RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "*", e2);
               }
            :}
            | term_arit:e1 DIVIDE factor_arit:e2 
            {: 
               RESULT = "NUMERIC"; 
               
               if (parser.codeGenerationEnabled) {
                   RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "/", e2);
               }
            :}
            | term_arit:e1 MOD factor_arit:e2 
            {: 
               RESULT = "INT"; 
               
               if (parser.codeGenerationEnabled) {
                   RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "%", e2);
               }
            :}
            | factor_arit:e {: RESULT = e; :}
            ;

factor_arit ::= factor_arit:e1 POW power_arit:e2 
              {: 
                 RESULT = "NUMERIC"; 
                 
                 if (parser.codeGenerationEnabled) {
                     RESULT = parser.getCodeGenerator().generateBinaryOp(e1, "**", e2);
                 }
              :}
              | power_arit:e {: RESULT = e; :}
              ;

power_arit ::= unary_exp:e {: RESULT = e; :}
             | atom_arit:e {: RESULT = e; :}
             ;

/* Operadores unarios con verificación de tipos */
unary_exp ::= MINUS atom_arit %prec UMINUS
            | INCREMENT ID
            | DECREMENT ID
            ;

/* Términos atómicos con tipos semánticos */
atom_arit ::= ID:id
            {: 
               if (parser.getSemanticTable() != null) {
                   parser.getSemanticTable().useVariable(id.toString(), idleft, idright);
               }
               
               if (parser.codeGenerationEnabled) {
                   RESULT = id.toString(); 
               } else {
                   RESULT = "VARIABLE"; 
               }
            :}
            | literal:l {: RESULT = l; :}
            | arr_access:a {: RESULT = a; :}
            | llamada_fun:f {: RESULT = f; :}
            | LPAREN exp:e RPAREN {: RESULT = e; :}
            ;

/* Lista de argumentos */
arg_list ::= exp
           | exp COMMA arg_list
           ;

/* Estructuras de control */
ctrl ::= if_stmt
       | do_while
       | for_stmt
       | switch_stmt
       ;

/* Estructura if-elif-else con scopes semánticos */
if_stmt ::= IF LPAREN exp:e RPAREN LBLOCK block RBLOCK
          {: 
             parser.enterControlScope("IF");
             parser.exitControlScope();
             
             if (parser.codeGenerationEnabled) {
                 parser.getCodeGenerator().addComment("Estructura IF procesada");
                 parser.getCodeGenerator().addComment("Condicion: " + e);
             }
          :}
          | IF LPAREN exp RPAREN LBLOCK block RBLOCK ELIF LPAREN exp RPAREN LBLOCK block RBLOCK
          {: 
             parser.enterControlScope("IF");
             parser.exitControlScope();
             
             if (parser.codeGenerationEnabled) {
                 parser.getCodeGenerator().addComment("Estructura IF-ELIF procesada");
             }
          :}
          | IF LPAREN exp RPAREN LBLOCK block RBLOCK ELSE LBLOCK block RBLOCK
          {: 
             parser.enterControlScope("IF");
             parser.exitControlScope();
             
             if (parser.codeGenerationEnabled) {
                 parser.getCodeGenerator().addComment("Estructura IF-ELSE procesada");
             }
          :}
          | IF LPAREN exp RPAREN LBLOCK block RBLOCK ELIF LPAREN exp RPAREN LBLOCK block RBLOCK ELSE LBLOCK block RBLOCK
          {: 
             parser.enterControlScope("IF");
             parser.exitControlScope();
             
             if (parser.codeGenerationEnabled) {
                 parser.getCodeGenerator().addComment("Estructura IF-ELIF-ELSE procesada");
             }
          :}
          | IF LPAREN error RPAREN LBLOCK block RBLOCK  {: System.err.println("Error en condición IF. Continuando..."); :}
          ;

/* Estructura do-while con scope semántico*/
do_while ::= DO LBLOCK block RBLOCK WHILE LPAREN exp:e RPAREN QUESTION
           {: 
              parser.enterControlScope("WHILE");
              parser.exitControlScope();
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().addComment("Estructura DO-WHILE procesada");
                  parser.getCodeGenerator().addComment("Condicion: " + e);
              }
           :}
           | DO LBLOCK block RBLOCK WHILE LPAREN error RPAREN QUESTION  {: System.err.println("Error en condición WHILE. Continuando..."); :}
           ;

/* Estructura for con scope semántico */
for_stmt ::= FOR LPAREN decl_var exp QUESTION exp RPAREN LBLOCK block RBLOCK
           {: 
              parser.enterControlScope("FOR");
              parser.exitControlScope();
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().addComment("Estructura FOR procesada con declaracion");
              }
           :}
           | FOR LPAREN assign exp QUESTION exp RPAREN LBLOCK block RBLOCK
           {: 
              parser.enterControlScope("FOR");
              parser.exitControlScope();
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().addComment("Estructura FOR procesada con asignacion");
              }
           :}
           | FOR LPAREN QUESTION exp QUESTION exp RPAREN LBLOCK block RBLOCK
           {: 
              parser.enterControlScope("FOR");
              parser.exitControlScope();
              
              if (parser.codeGenerationEnabled) {
                  parser.getCodeGenerator().addComment("Estructura FOR procesada sin inicializacion");
              }
           :}
           | FOR LPAREN error RPAREN LBLOCK block RBLOCK  {: System.err.println("Error en estructura FOR. Continuando..."); :}
           ;

/* Estructura switch */
switch_stmt ::= SWITCH LPAREN exp RPAREN LBLOCK case_list RBLOCK
              {: 
                 if (parser.codeGenerationEnabled) {
                     parser.getCodeGenerator().addComment("Estructura SWITCH procesada");
                 }
              :}
              | SWITCH LPAREN error RPAREN LBLOCK case_list RBLOCK  {: System.err.println("Error en expresión SWITCH. Continuando..."); :}
              ;

/* Lista de casos */
case_list ::= case_stmt case_list
           | default_stmt
           | /* vacío */
           ;

/* Caso individual */
case_stmt ::= CASE exp COLON block;

/* Caso por defecto */
default_stmt ::= DEFAULT COLON block;

/* Sentencia break */
break_stmt ::= BREAK QUESTION
             {: 
                if (parser.codeGenerationEnabled) {
                    parser.getCodeGenerator().generateBreak();
                }
             :}
             ;

/* Sentencia return con verificación de tipos */
return_stmt ::= RETURN exp:e QUESTION
              {: 
                 System.out.println("DEBUG: Return con expresión, función actual: " + parser.getCurrentFunctionName());
                 
                 if (parser.getSemanticTable() != null && parser.getCurrentFunctionName() != null) {
                     parser.getSemanticTable().checkReturnStatement(parser.getCurrentFunctionName(), e, eleft);
                 } else {
                     System.err.println("ERROR: No se puede verificar return - función actual: " + parser.getCurrentFunctionName());
                 }
                 
                 if (parser.codeGenerationEnabled) {
                     parser.getCodeGenerator().generateReturn(e);
                 }
              :}
              | RETURN:r QUESTION
              {: 
                 System.out.println("DEBUG: Return sin expresión, función actual: " + parser.getCurrentFunctionName());
                 
                 if (parser.getSemanticTable() != null && parser.getCurrentFunctionName() != null) {
                     parser.getSemanticTable().checkReturnStatement(parser.getCurrentFunctionName(), null, rleft);
                 } else {
                     System.err.println("ERROR: No se puede verificar return - función actual: " + parser.getCurrentFunctionName());
                 }
                 
                 if (parser.codeGenerationEnabled) {
                     parser.getCodeGenerator().generateReturn(null);
                 }
              :}
              | RETURN error QUESTION  {: System.err.println("Error en expresión RETURN. Continuando..."); :}
              ;

/* Funciones de entrada/salida */
read_stmt ::= READ LPAREN ID:id RPAREN QUESTION
            {: 
               if (parser.getSemanticTable() != null) {
                   parser.getSemanticTable().useVariable(id.toString(), idleft, idright);
               }
               
               if (parser.codeGenerationEnabled) {
                   parser.getCodeGenerator().generateRead(id.toString());
               }
            :}
            | READ LPAREN error RPAREN QUESTION  {: System.err.println("Error en función READ. Continuando..."); :}
            ;

write_stmt ::= WRITE LPAREN exp:e RPAREN QUESTION
             {: 
                
                if (parser.codeGenerationEnabled) {
                    parser.getCodeGenerator().generateWrite(e);
                }
             :}
             | WRITE LPAREN error RPAREN QUESTION  {: System.err.println("Error en función WRITE. Continuando..."); :}
             ;